CLASS lhc_booking DEFINITION INHERITING FROM cl_abap_behavior_handler.
  PRIVATE SECTION.

    METHODS calculateBookingID FOR DETERMINE ON MODIFY
      IMPORTING keys FOR booking~calculateBookingID.

    METHODS calculateTotalPrice FOR DETERMINE ON MODIFY
      IMPORTING keys FOR booking~calculateTotalPrice.

ENDCLASS.

CLASS lhc_booking IMPLEMENTATION.

  METHOD calculateBookingID.

    DATA: max_booking_id TYPE /dmo/booking_id.

*-> What is this??? Something to update via an association
    DATA: update TYPE TABLE FOR UPDATE zi_travel01\\booking.



* -> read all travels related to the Booking.
* -> If multiple bookings belong to the same travel are requested, the travel is returned only once
    READ ENTITIES OF zi_travel01 IN LOCAL MODE
     ENTITY booking BY \_travel
     FIELDS ( Traveluuid )
     WITH CORRESPONDING #( keys )
    RESULT DATA(travels).

    LOOP AT travels ASSIGNING FIELD-SYMBOL(<lfs_travel>).

*-> Process all affected travels and read respective bookings.
      READ ENTITIES OF zi_travel01 IN LOCAL MODE
       ENTITY travel BY \_booking
      FIELDS ( BookingId )
      WITH VALUE #( ( %tky = <lfs_travel>-%tky ) )
      RESULT DATA(bookings).

      max_booking_id = '0000'.
      LOOP AT bookings INTO DATA(booking).
        IF booking-BookingId > max_booking_id .
          max_booking_id = booking-BookingId.
        ENDIF.
      ENDLOOP.

      LOOP AT bookings INTO booking WHERE BookingId IS INITIAL.
        max_booking_id += 10.

        APPEND VALUE #( %tky = booking-%tky
                        BookingId = max_booking_id  ) TO update.

      ENDLOOP.
    ENDLOOP.

    MODIFY ENTITIES OF zi_travel01 IN LOCAL MODE
     ENTITY booking
      UPDATE
      FIELDS ( BookingId  ) WITH update
      REPORTED DATA(update_reported).

    reported = CORRESPONDING #( DEEP update_reported ).

  ENDMETHOD.

  METHOD calculateTotalPrice.
*-> Read All Travels for the requested Booking
    READ ENTITIES OF zi_travel01 IN LOCAL MODE
     ENTITY booking BY \_travel
     FIELDS ( Traveluuid )
     WITH CORRESPONDING #( keys )
     RESULT DATA(travels)
     FAILED DATA(failed).

*-> calling the internal action to modify
    MODIFY ENTITIES OF zi_travel01 IN LOCAL MODE
     ENTITY travel
     EXECUTE recalcTotalPrice
     FROM CORRESPONDING #( travels )
     REPORTED DATA(execute_reported).

    reported = CORRESPONDING #( DEEP execute_reported )  .

  ENDMETHOD.

ENDCLASS.