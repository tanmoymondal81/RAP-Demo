@EndUserText.label: 'Travel BO Projection View'
@AccessControl.authorizationCheck: #CHECK
// Adding some service specific semantics to the prjection view
// Allowing the projection view to be enhanced with metadata extension
@Metadata.allowExtensions: true
// Allowing Search
@Search: {
    searchable: true
}

define root view entity ZC_TRAVEL01
  as projection on zi_travel01 as travel
{
  key Traveluuid,

      // allow search for TravelID
      @Search: {
          defaultSearchElement: true
      }
      TravelId,

      // allow search for AgencyID
      @Search.defaultSearchElement: true
      // AgencyName is the text description for AgencyID
      // this is achieved using the below annotation
      @ObjectModel: { text: {
              element: ['AgencyName']
                   }
                 }
      //value help for Agency ID using the below Annotation
      @Consumption: {
          valueHelpDefinition: [{
              entity: {
                  name: '/DMO/I_Agency',
                  element: 'AgencyID'
              }
          }]
      }
      AgencyId,
      _agency.Name       as AgencyName,

      // allow search for CustomerID
      @Search.defaultSearchElement: true
      // CustomerName is the text description for CustomerID
      // this is achieved using the below annotation
      @ObjectModel.text.element: ['CustomerName']
      //value help for Customer ID
      @Consumption.valueHelpDefinition: [{
          entity: {
              name: '/DMO/I_Customer',
              element: ' CustomerID '
          }
      }]
      CustomerId,
      _customer.LastName as CustomerName,
      BeginDate,
      EndDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      BookingFee,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      TotalPrice,
      //value help for currency code
      @Consumption.valueHelpDefinition: [{
          entity: {
              name: 'I_Currency',
              element: ' Currency'
          }
       }]
      CurrencyCode,
      Description,
      OverallStatus,
      CreatedBy,
      CreatedAt,
      LastChangedBy,
      LastChangedAt,
      LocalLastChangedAt,

      /* Associations */
      _agency,
      // redirection of the booking child BO entity to the booking projection view
      _booking : redirected to composition child ZC_BOOK01,
      _currency,
      _customer
}
